#! /usr/bin/awk -f
#
# Test Environment Builder
#
# Script for symbol table generation.
##
# Copyright (C) 2016 Test Environment authors (see file AUTHORS in
# the root directory of the distribution).
#
# Test Environment is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 2 of
# the License, or (at your option) any later version.
#
# Test Environment is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston,
# MA  02111-1307  USA
#
#
# @author Artem Andreev <Artem.Andreev@oktetlabs.ru>
#
# $Id$
#

BEGIN {
    FS = "|";
    if (!TABLE_NAME)
        TABLE_NAME = "generated_table";
    print "#include \"agentlib_defs.h\""
}

NF != 7 { next }

{
    gsub(/[[:space:]]+/, "", $1);
    gsub(/[[:space:]]+/, "", $3);
    gsub(/[[:space:]]+/, "", $4);

    # Special hack is needed for DPDK symbols
    if ($1 ~ /[^[:alnum:]_]/ || $1 ~ /^per_lcore__/) next

    if ($3 == "T" || (($3 == "D" || $3 == "B") && $4 == "FUNC"))
    {
        SYMBOLS[$1] = "TRUE";
        printf "extern void %s(void);\n", $1
    }
    else if (($3 == "D" || $3 == "B") && $4 != "FUNC")
    {
        SYMBOLS[$1] = "FALSE";
        printf "extern int %s;\n", $1
    }
}

END {
    print "rcf_symbol_entry", TABLE_NAME, "[] = {"
    for (sym in SYMBOLS)
    {
        printf "\t{.name = \"%s\", .addr = (void *)(&%s), .is_func = %s},\n", sym, sym, SYMBOLS[sym];
    }
    print "\t{.name = NULL, .addr = NULL}"
    print "};"
}




