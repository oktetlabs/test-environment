#! /usr/bin/awk -f
# SPDX-License-Identifier: Apache-2.0
#
# Test Environment Builder
#
# Script for symbol table generation.
##
# Copyright (C) 2004-2022 OKTET Labs Ltd. All rights reserved.

BEGIN {
    FS = "|";
    if (!TABLE_NAME)
        TABLE_NAME = "generated_table";
    print "#include \"agentlib_defs.h\""
}

NF != 7 { next }

{
    gsub(/[[:space:]]+/, "", $1);
    gsub(/[[:space:]]+/, "", $3);
    gsub(/[[:space:]]+/, "", $4);

    # Special hack is needed for DPDK symbols
    if ($1 ~ /[^[:alnum:]_]/ || $1 ~ /^per_lcore__/) next

    if ($3 == "T" || (($3 == "D" || $3 == "B" || $3 == "C") && $4 == "FUNC"))
    {
        SYMBOLS[$1] = "TRUE";
        printf "extern void %s(void);\n", $1
    }
    else if (($3 == "D" || $3 == "B" || $3 == "C") && $4 != "FUNC")
    {
        SYMBOLS[$1] = "FALSE";
        printf "extern int %s;\n", $1
    }
}

END {
    print "rcf_symbol_entry", TABLE_NAME, "[] = {"
    for (sym in SYMBOLS)
    {
        printf "\t{.name = \"%s\", .addr = (void *)(&%s), .is_func = %s},\n", sym, sym, SYMBOLS[sym];
    }
    print "\t{.name = NULL, .addr = NULL}"
    print "};"
}




