#! /usr/bin/awk -f

# This script extracts comments from a `package.xml' file and converts them
# to the form suitable for Doxygen processing

/\/\*\*.*@/ { 
    if (header_finished == 1) 
    {
        print "*/\n";
    }
    header_finished = 0;
}
/\/\*\*/ && !header_finished { in_header = 1; print; next; }

in_header && /[[:space:]]*@author/ {
    author = $0;
    next;
}
in_header && /[[:space:]]\*[ \t\n]*$/ { next; }
in_header && /\*\// { in_header = 0; header_finished = 1; next; }
in_header { print; next; }

/\/\*-/ { sub(/^.*\/\*-/, ""); sub(/^/, " -#"); sub(/-#-/, "  -#");
    in_dox_comment = 1; 
    was_comment = 1;
    printf " *";
}

in_dox_comment && /\*\// { sub(/\*\/.*/, "");
    in_dox_comment = 0; 
    if ($0 ~ /^[[:space:]]*$/) next;
    if ($0) print; 
    next; 
}

in_dox_comment { print $0; next }

END { printf " *\n" author "\n */\n";}

    
