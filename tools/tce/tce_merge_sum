#! /usr/bin/awk -f
#
# Test Coverage Estimation
#
# Script to merge TCE results from different sources
#
#
# Copyright (C) 2007 Test Environment authors (see file AUTHORS in
# the root directory of the distribution).
#
# Test Environment is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation; either version 2 of
# the License, or (at your option) any later version.
#
# Test Environment is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston,
# MA  02111-1307  USA
#
#
# Author Artem V. Andreev <Artem.Andreev@oktetlabs.ru>
#
# $Id$
#

BEGIN {
    lines = 0;
    executed = 0;
    nbranches = 0;
    exbranches = 0;
}

NF != 8 { 
    print "master and delta files are incompatible" >"/dev/stderr";
    exit 1
}

{
    if ($1 == "-")
        $1 = $5;
    else if ($5 != "-")
        $1 = $1 + $5
    if ($2 == "*")
        $2 = $6;
    else if ($6 != "*")
    {
        split($2, f1, ":");
        split($6, f2, ":");
        total = f1[1] + f2[1]
        tp = (f1[2] * f1[1] + f2[2] * f2[1]) / total
        $2 = total ":" tp ":" (f1[3] + 0 > f2[3] + 0 ? f1[3] : f2[3]);
    }
    if ($3 == "*")
        $3 = $7;
    else if ($7 != "*")
    {
        split($3, b1, ":");
        split($7, b2, ":");
        $3 = ""
        for (i = 1; i in b1 || i in b2; i++)
        {
            $3 = $3 (b1[i] + b2[i]) ":";
            nbranches++;
            if (b1[i] + b2[i] + 0 != 0) exbranches++;
        }
        sub(/:$/, "", $3);
    }
    if ($4 == "*")
        $4 = $8;
    else if ($8 != "*")
    {
        split($4, c1, ":");
        split($8, c2, ":");
        $4 = ""
        for (i = 1; i in c1 || i in c2; i++)
        {
            $4 = $4 (c1[i] + c2[i]) ":"
        }
        sub(/:$/, "", $4);
    }
    if ($1 != "-")
    {
        lines++;
        if ($1 + 0 != 0)
            executed++;
    }
    $5 = "";
    $6 = "";
    $7 = "";
    $8 = "";
    print
}

END {
    print "SUMMARY", lines, executed, nbranches, exbranches
}
