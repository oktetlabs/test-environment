#! /usr/bin/awk -f
#
# Test Coverage Estimation
#
# Script to merge TCE results from different sources
#
#
# Copyright (C) 2004-2018 OKTET Labs.
#
# Author Artem V. Andreev <Artem.Andreev@oktetlabs.ru>
#
#

BEGIN {
    lines = 0;
    executed = 0;
    nbranches = 0;
    exbranches = 0;
}


NF != 8 {
    print "master and delta files are incompatible" >"/dev/stderr";
    exit 1
}

{
    if ($1 == "-")
    {
        if ($5 != "-") lines++;
        $1 = $5;
    }
    else if ($5 == "-")
        $1 = "-0";
    else
    {
        lines++;
        if ($1 == $5)
        {
            if ($2 SUBSEP $3 SUBSEP $4 != $6 SUBSEP $7 SUBSEP $8)
            {
                $1 = "+0";
            }
            else
            {
                $1 = "-";
            }
        }
        else
        {
            executed++;
            $1 = $5 - $1;
        }
    }

    if ($2 == "*")
        $2 = $6;
    else if ($6 != "*")
    {
        split($2, f1, ":");
        split($6, f2, ":");
        total = f1[1] + f2[1]
        $2 = (f2[1] - f1[1]) ":" (f2[2] - f1[2]) ":" (f2[3] - f1[3]);
    }
    if ($3 == "*")
        $3 = $7;
    else if ($7 != "*")
    {
        split($3, b1, ":");
        split($7, b2, ":");
        $3 = ""
        for (i = 1; i in b1 || i in b2; i++)
        {
            $3 = $3 (b2[i] - b1[i]) ":";
            nbranches++;
            if (b2[i] + 0 != b1[i]) exbranches++;
        }
        sub(/:$/, "", $3);
    }
    if ($4 == "*")
        $4 = $8;
    else if ($8 != "*")
    {
        split($4, c1, ":");
        split($8, c2, ":");
        $4 = ""
        for (i = 1; i in c1 || i in c2; i++)
        {
            $4 = $4 (c2[i] - c1[i]) ":"
        }
        sub(/:$/, "", $4);
    }
    $5 = "";
    $6 = "";
    $7 = "";
    $8 = "";
    print
}

END {
    print "SUMMARY", lines, executed, nbranches, exbranches
}
